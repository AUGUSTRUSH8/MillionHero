import console;
import key;
import process;
import process.popen;
import process.adb;
import fsys;
import fsys.file;
import fsys.stream;
import gdip;
import web.json;
import web.rest.client;
import crypt.bin; 
import string.xml;
import web.mshtml;
import time.ole;
import inet.url;
import string.html;

console.setTitle("MillionDollar");
var imgpath = "C:\screenshot\screenshot.png";
var keypath = "C:\screenshot\token.txt";
var api = "http://text.aliapi.hanvon.com/rt/ws/v1/ocr/text/recg?code=74e51a88-41ec-413e-b162-bd031fe0407e";
var appcode = "xxxxx";
var bdorcapi = "https://aip.baidubce.com/rest/2.0/ocr/v1/general_basic";
var apiKey = "QMC9Tp8ww8yjHDXI258uVOlM";
var secretKey = "LOko3wu9ZbVXMUyribzxGkivkIf6SbMR";
var token_url = "https://aip.baidubce.com/oauth/2.0/token";
var bd_token = "";
var now = tonumber(time.now());


//api获取token
var to_auth = function(){
	var apiClient = web.rest.client();
	var params = {
		grant_type = "client_credentials";
		client_id = apiKey;
		client_secret = secretKey;
	}
	var json = apiClient.get(token_url,params);
	var resp = web.json.parse(json);
	resp.expires_in = now + resp.expires_in;
	bd_token = resp.access_token;
	var keyfile = fsys.file(keypath,"w+");
	json = web.json.stringify(resp);
	keyfile.write(json);
	keyfile.close();
	//console.dump(resp);
}


//处理过期token
var get_auth = function(){
    if(!fsys.searchFile(keypath))
	{
		to_auth();
    }else {
        var keyfile = fsys.file(keypath,"r+");
        var json = keyfile.readAll();
        var resp = web.json.parse(json);
    	if(resp.expires_in == null || now > resp.expires_in){
    		to_auth();
    	}else {
    		bd_token = resp.access_token;
    	}
    	
    	keyfile.close();
    }
}




//安卓截屏
var jieping = function(){
    fsys.delete(imgpath);
    process.adb("shell rm /sdcard/screenshot.png" );
    sleep(100);
	process.adb("shell /system/bin/screencap -p /sdcard/screenshot.png" );
	//process.adb.pull( "/sdcard/screenshot.png",imgpath ) 
	while(true){ 
		if(fsys.searchFile(imgpath))
		{
			var files = fsys.file(imgpath);
			var file_size = files.size();
			//console.log(file_size);
			if(file_size>0)
			{
				files.close();
				break ;
			}
		}
		process.adb.pull( "/sdcard/screenshot.png",imgpath )
		sleep(100); 
	}
	
	//console.log("截屏成功")
}

//截屏裁剪
var caijian = function(){
	//80 250 550
	var bitmap = gdip.bitmap(imgpath );
	var bitmagNew = gdip.bitmap(920,1120);
	var graphics = bitmagNew.getGraphics();
	graphics.drawImageRectRect(bitmap,0,0,920,1120,80,250,920,1120)
	bitmagNew.save("C:\screenshot\screenshot_block.png")
	//console.log("截取问题成功")
	
}

//百度免费每天500orc识别
var bdorc = function(){
	var apiClient = web.rest.client();
	var headers  ={
		['Content-Type'] = "application/json; charset=UTF-8";
		['Content-Type'] = "application/x-www-form-urlencoded";
	}
	var streams = fsys.stream("C:\screenshot\screenshot_block.png","r+");
	var content = streams.readAll();
	var params = {
		access_token = bd_token;
		image = crypt.bin.encodeBase64(content);
	}
	var json = apiClient.post(bdorcapi,params);
	var resp = web.json.parse(json);
	//console.log(json);
	var question = "";
	var answer = {};
	if(resp.words_result==null)
	{
		console.log("识别失败："+resp.result);
		return null; 
	}
	var i = 1;
	var num = resp.words_result_num;
	for(key,val in resp.words_result){
		if(num>4)
		{
			if(i<=2)
			{
				question = question+""+val.words
			}else {
				table.push(answer,val.words);
			}
		}else {
			if(i==1)
			{
				question = question+""+val.words
			}else {
				table.push(answer,val.words);
			}
		}
		i++;
	}
	console.log(question);
	//除去序号1.2.3.
	for( i =1;12;1){
		question = string.replace(question,string.concat(i,"."),"");
	}
	//console.dump(answer);
	var obj = {
		question = question;
		answer = answer;
	}
	return obj; 
}



//orc识别
var orcshibie = function(){
	var apiClient = web.rest.client();
	var headers  ={
		['Content-Type'] = "application/json; charset=UTF-8";
		['Content-Type'] = "application/octet-stream";
		['Authorization'] = "APPCODE " + appcode;
	}
	apiClient.addHeaders = headers;
	var streams = fsys.stream("C:\screenshot\screenshot_block.png","r+");
	var content = streams.readAll();
	var params = {
		uid = "118.12.0.12";
		color = "color";
		lang = "chns";
		image = crypt.bin.encodeBase64(content);
	}
	
	var json = apiClient.post(api,web.json.stringify(params,true));
	var resp = web.json.parse(json);
	//console.dump(resp);
	
	if(resp.code == "0"){
		var question = resp.textResult;
		question = string.replace(question,"\r","");
		question = string.replace(question,"\n","");
		question = string.replace(question," ","");
		console.log(question);
		return question; 
	}else {
		console.log("识别失败："+resp.result)
		return null; 
	}
	

}
var search = function(obj){
	question = obj.question;
	answer = obj.answer
	console.log("");
	//console.dump(string.find(question,"不是"));
	if(string.find(question,"不是")||string.find(question,"不含")||string.find(question,"不包")||string.find(question,"不属")||string.find(question,"不在")||string.find(question,"没有"))
	{
		console.log("*此题可能是否定题，建议选统计次数最少的答案*");
	}else {
		console.log("*建议选统计次数最多的答案*");
	}
	
	
	question = inet.url.encode(question)
	html = web.mshtml();
	html.document.designMode = "on";
	url = "https://www.baidu.com/s?wd="+question;
	html.go(url);
	var content = html.html;
	content = string.html.toText(content);
	//console.log(content);
	//qEle = html.queryEles(className = "c-abstract");
	//console.log("");
	//console.log(qEle[1].innerText);
	console.log("");
	console.log("统计答案出现次数：")
	console.log("");

	for(key,val in answer){
		//console.log(val);
		strlist = string.split(content,string.concat("<",val,">"));
		console.log(string.concat(val," ",(#strlist-1),"个"));
	}
	
} 


var init = function(){
	//关闭adb进程
	var proc = "adb.exe";
	for processEntry in process.each(proc) {
		process.kill(processEntry.szExeFile)
	}
	fsys.createDir("c:/screenshot/");
	get_auth();//获取百度token
	
	process.adb.killServer();
	process.adb.startServer();
	if( process.adb.getState() != "device" ){
			console.log("请选连接安卓手机,并打开设置->开发者选项->USB调试模式" )
			console.pause()
			return ; 
	}
	console.log("设备串号",process.adb.getSerialno() )
	console.log("按回车键开始识别问题..." );
}


init();

while(true){ 
	if( console.kbHit() ){
		var kb = console.kbRead();
		if(!kb) continue;
		if( kb.wVirtualKeyCode == 0x1B/*_VK_ESC*/ ){
			process.adb.killServer();
			break;
		}
		if(kb.bKeyDown==1)
		{
			jieping();
			caijian();
			//var question = orcshibie();
			//var question = '岁寒三友有哪些？';
			var question = bdorc();
			if(!question){
				continue ;
			}	
			search(question);
			console.log(" ")
			console.log("按回车键开始识别下一道题..." );
		}
		 
	}
}


